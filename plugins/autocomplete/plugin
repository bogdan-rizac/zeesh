# zeesh zsh autocomplete plugin

# symlink _brew if on osx
if [ -f "/usr/local/Library/Contributions/brew_zsh_completion.zsh" ] && [ ! -f "$plugin_dir/comp/_brew" ]; then
    ln -s /usr/local/Library/Contributions/brew_zsh_completion.zsh $plugin_dir/comp/_brew
fi

# add zeesh comps to $fpath
fpath=( $plugin_dir/comp $fpath )

autoload -U compinit
compinit -u -d ~/.zsh/cache/zcompdump

_force_rehash()
{
    (( CURRENT == 1 )) && rehash
    return 1
}

zstyle ':completion:*' completer _force_rehash _complete _ignored _match _correct _approximate _prefix
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zsh/cache

zstyle ':completion:*:*:open:*' matcher 'm:{a-z}={A-Z} r: ||[^ ]=**'

# case-insensitive (all), partial-word and then substring completion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' list-colors ''
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'

# zshfu plugin list autocomplete
compctl -K zeesh-plugin-list zeesh-plugin

# hide shit
zstyle ':completion:*:*' ignored-patterns '*.zwc' '*.old' 'broadcasthost' '*?.pyc'
